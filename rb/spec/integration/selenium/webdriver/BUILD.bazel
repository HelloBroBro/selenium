load("@rules_ruby//ruby:defs.bzl", "rb_library")
load("//rb/spec:tests.bzl", "rb_integration_test")

rb_library(
    name = "spec_helper",
    testonly = True,
    srcs = [
        "spec_helper.rb",
        "spec_support.rb",
    ] + glob(["spec_support/**/*"]),
    data = [
        "//rb:manager-linux",
        "//rb:manager-macos",
        "//rb:manager-windows",
    ] + select({
        "//rb/spec/integration:remote": ["//java/src/org/openqa/selenium/grid:selenium_server_deploy.jar"],
        "//conditions:default": [],
    }),
    visibility = ["//rb/spec:__subpackages__"],
    deps = [
        "//rb/lib:selenium-webdriver",
        "//rb/lib/selenium:devtools",
        "//rb/lib/selenium:server",
        "//rb/lib/selenium:webdriver",
        "//rb/lib/selenium/webdriver:atoms",
        "//rb/lib/selenium/webdriver:common",
        "//rb/lib/selenium/webdriver:remote",
        "//rb/spec:rspec_matchers",
        "@bundle",
    ] + select({
        "//rb/spec/integration:edge": ["//rb/lib/selenium/webdriver:edge"],
        "//rb/spec/integration:firefox": ["//rb/lib/selenium/webdriver:firefox"],
        "//rb/spec/integration:ie": ["//rb/lib/selenium/webdriver:ie"],
        "//rb/spec/integration:safari": ["//rb/lib/selenium/webdriver:safari"],
        "//rb/spec/integration:safari-preview": ["//rb/lib/selenium/webdriver:safari"],
        "//conditions:default": ["//rb/lib/selenium/webdriver:chrome"],
    }),
)

[
    rb_integration_test(
        name = file[:-8],
        srcs = [file],
        deps = [
            "//rb/lib/selenium/webdriver:common",
        ],
    )
    for file in glob(
        ["*_spec.rb"],
        exclude = [
            "bidi_spec.rb",
            "driver_spec.rb",
            "devtools_spec.rb",
            "element_spec.rb",
        ],
    )
]

rb_integration_test(
    name = "bidi",
    srcs = ["bidi_spec.rb"],
    tags = ["exclusive-if-local"],
    deps = [
        "//rb/lib/selenium/devtools",
        "//rb/lib/selenium/webdriver:bidi",
    ],
)

rb_integration_test(
    name = "devtools",
    srcs = ["devtools_spec.rb"],
    tags = ["exclusive-if-local"],
    deps = [
        "//rb/lib/selenium/devtools",
        "//rb/lib/selenium/webdriver:bidi",
    ],
)

rb_integration_test(
    name = "driver",
    srcs = ["driver_spec.rb"],
    deps = [
        "//rb/lib/selenium/webdriver:chrome",
        "//rb/lib/selenium/webdriver:edge",
        "//rb/lib/selenium/webdriver:firefox",
        "//rb/lib/selenium/webdriver:ie",
        "//rb/lib/selenium/webdriver:remote",
        "//rb/lib/selenium/webdriver:safari",
    ],
)

rb_integration_test(
    name = "element",
    srcs = ["element_spec.rb"],
    deps = [
        "//rb/lib/selenium/webdriver:chrome",
        "//rb/lib/selenium/webdriver:edge",
        "//rb/lib/selenium/webdriver:firefox",
        "//rb/lib/selenium/webdriver:ie",
        "//rb/lib/selenium/webdriver:remote",
        "//rb/lib/selenium/webdriver:safari",
    ],
)
